@Metadata.layer: #CORE
@UI: {
     headerInfo: {
        typeName: 'Travel',
        typeNamePlural: 'Travels',
        title: {
            type: #STANDARD, value: 'Description'
        },
        description: {
            value: 'TravelID'
        }
    },
    presentationVariant: [{
        sortOrder: [{
            by: 'LocalLastChangedAt',
            direction: #DESC
        }],
        visualizations: [{
            type: #AS_LINEITEM
        }]
    }]
}
annotate view ZC_FE_TRAVEL_000258 with
{

  @UI.facet: [
  
        /* Header Facets */
        {
            id: 'TravelHeaderPrice',
            type: #DATAPOINT_REFERENCE,
            purpose: #HEADER,
            position: 10,
            targetQualifier: 'PriceData'
        },
        {
            id: 'TravelHeaderOverallStatus',
            type: #DATAPOINT_REFERENCE,
            purpose: #HEADER,
            position: 20,
            targetQualifier: 'StatusData'
         },
         
          /* Standard Facets */
          //Section 'General Information'
         {
            label: 'General Information',
            id: 'GeneralInfo',
            type: #COLLECTION,
            position: 10
         },
         //Field Group 'General' which contains all the identification fields
         {
            label: 'General',
            id: 'Travel',
            type: #IDENTIFICATION_REFERENCE,
            purpose: #STANDARD,
            parentId: 'GeneralInfo',
            position: 10
          },
         //Field Group 'Prices'
          {
            label: 'Prices',
            id: 'Prices',
            type: #FIELDGROUP_REFERENCE,
            purpose: #STANDARD, 
            parentId: 'GeneralInfo',
            position: 20,
            targetQualifier: 'PricesGroup'
          },
          //Field Group 'Dates'
          {
            label: 'Dates',
            id: 'Dates',
            type: #FIELDGROUP_REFERENCE,
            purpose: #STANDARD,
            parentId: 'GeneralInfo',
            position: 30,
            targetQualifier: 'DatesGroup'
          },

          //Section 'Bookings'
          {
            label: 'Bookings',
            id: 'Booking',
            type: #LINEITEM_REFERENCE,
            purpose: #STANDARD,
            position: 20,
            targetElement: '_Booking'
           }
    ]

  @UI.lineItem: [{position: 10 }]
  TravelID;


  @UI.identification: [{ position: 10 }]
  Description;

  @UI.lineItem: [{position: 30 }]
  @UI.selectionField: [{ position : 10 }]
  @UI.identification: [{ position: 30 }]
  AgencyID;

  @UI.lineItem: [{position: 40 }]
  @UI.selectionField: [{ position : 20 }]
  @UI.identification: [{ position: 20 }]
  CustomerID;

  @UI.lineItem: [{position: 50 }]
  @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 10 }]
  BeginDate;

  @UI.lineItem: [{position: 60 }]
  @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 20 }]
  EndDate;

  @UI.lineItem: [{position: 70 }]
  @UI.fieldGroup: [ { qualifier: 'PricesGroup', position: 10} ]
  BookingFee;

  @UI.lineItem: [{position: 80 }]
  @UI.dataPoint: { qualifier: 'PriceData', title: 'Total Price'}
  @UI.fieldGroup: [{ qualifier: 'PricesGroup', position: 20 }]
  TotalPrice;
 
  @UI.lineItem: [{position: 90, criticality: 'OverallStatusCriticality' }]
  @UI.selectionField: [{ position : 30 }]
  @UI.textArrangement: #TEXT_ONLY
  @UI.dataPoint: { qualifier: 'StatusData' , title: 'Status' , criticality: 'OverallStatusCriticality'}
  OverallStatus;

  @UI.lineItem: [{position: 100 }]
  LocalLastChangedAt;


}